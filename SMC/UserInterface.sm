%{ //
//
%}

%class UserInterface
%header UserInterface.h
%fsmclass UserInterfaceFSM
%fsmfile UserInterfaceFSM
%start UIMap::Reset 
%map UIMap
%%

Reset
{
    Ready UITest {}
  
}

UITest
Entry
{
// Enable all LEDs
uiLedPower(1);
uiLedStatus(1);
uiDisplay(888);
m_timer.setTimeout(500);
}
Exit
{
// Disable all LEDs
uiLedPower(0);
uiLedStatus(0);
uiDisplay("   ");
}
{
    timerElapsed AddressDisplay {}
}

AddressDisplay

Entry
{
uiDisplay(ctxt.m_dmxAddress);
m_timer.setPeriod(50);
}
Exit
{
m_timer.Stop();
}
{
    timerElapsed nil
    {
       uiDisplay(ctxt.m_dmxAddress);
    }
    BtnModePressed MenuMode {}
  
}

MenuMode
Entry
{
uiDisplay("mod");
}
{
BtnOkPressed MenuModeDmx {}
BtnModePressed MenuSpeed {}
}

MenuSpeed
Entry
{
uiDisplay("spd");
}
{
BtnOkPressed MenuSpeedShow {}
BtnModePressed MenuDisplay {}
}

MenuDisplay
Entry
{
uiDisplay("dsp");
}
{
BtnOkPressed MenuDisplayShow {}
BtnModePressed MenuExit {}
}

MenuExit
Entry
{
uiDisplay("ext");
}
{
BtnOkPressed AddressDisplay {}
BtnModePressed MenuMode {}
}

MenuModeDmx
Entry
{
uiDisplay("dmx");
}
{
BtnModePressed MenuModeRainBow {}
BtnOkPressed AddressDisplay { setMode(UserInterface::OperationDmx); }
}

MenuModeRainBow
Entry
{
uiDisplay("rai");
}
{
BtnModePressed MenuModeTest {}
BtnOkPressed AddressDisplay { setMode(UserInterface::OperationRainbow); }
}

MenuModeTest
Entry
{
uiDisplay("tst");
}
{
BtnModePressed MenuModeDmx {}
BtnOkPressed AddressDisplay { setMode(UserInterface::OperationTest); }
}

MenuSpeedShow
Entry
{
uiDisplay(ctxt.getSpeed());
}
{
BtnModePressed MenuSpeedIncrease {  }
BtnOkPressed AddressDisplay { }
}

MenuSpeedIncrease
Entry
{
incSpeed();
}
{
Default MenuSpeedShow {}
}

MenuDisplayShow
Entry
{
uiDisplay(ctxt.getDisplayBrightness());
}
{
BtnModePressed MenuDisplayIncrease { }
BtnOkPressed AddressDisplay { }
}

MenuDisplayIncrease
Entry
{
 incDisplayBrightness(); 
}
{
Tick MenuDisplayShow { }
}



Default
{
    Ready nil {}
    Tick nil {}
    Default nil {}
}

%%

